plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id "org.beryx.jlink" version "2.24.0"
}

import org.apache.tools.ant.taskdefs.condition.Os

group 'rr.industries'
version '0.0.5'

repositories {
    mavenCentral()
}

ext {
    isWindows = Os.isFamily(Os.FAMILY_WINDOWS)
}

mainClassName = 'rr.industries.EndlessSkyViewer'

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

javafx {
    version = "16"
    modules = [ 'javafx.controls' ]
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageName = 'endless-sky-viewer'
    jpackage {
        installerName = 'endless-sky-viewer'
        installerOptions = project.isWindows ? ['--win-menu', '--win-shortcut'] : []
    }
    mergedModule {
        requires "java.management"
        requires "java.naming"
        requires "java.logging"
        requires "java.sql"
        requires "java.xml"
        requires "java.desktop"
        requires "java.datatransfer"
        requires "jdk.unsupported"
    }
}

tasks.register('zipJPackage', Zip) {
    dependsOn jpackage
    destinationDir = project.file(project.buildDir.path + '/jpackage')
    String osName = (project.isWindows ? 'win' : 'linux')
    archiveFileName = 'endless-sky-viewer-' + project.version + '-' + osName + '.zip'

    from project.buildDir.path + '/jpackage/endless-sky-viewer'
}

build {
    dependsOn jpackage
    dependsOn zipJPackage
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'net.harawata', name: 'appdirs', version: '1.2.1'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'

}
